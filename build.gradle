plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'org.jetbrains.kotlin.jvm'
    id 'com.gradleup.shadow' version '9.0.0-rc3'
}

group = 'xyz.devvydont'
version = System.getenv("RELEASE_TAG") ?: "dev"

jar {
    archiveBaseName.set("SMPRPG")
    archiveVersion.set(version.toString())
}

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly 'net.dmulloy2:ProtocolLib:5.4.0'
    implementation 'com.google.code.gson:gson:2.13.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks {
    shadowJar {
        relocate("kotlin", "xyz.devvydont.libs.kotlin")
        relocate("kotlinx", "xyz.devvydont.libs.kotlinx")
    }
    build {
        dependsOn(shadowJar)
    }
    runServer {
        minecraftVersion("1.21.11")
        jvmArgs("-XX:+UseParallelGC")
    }
}

tasks.register('zipResourcePack', Zip) {
    from 'resourcepack'
    archiveFileName = 'resourcepack.zip'
    destinationDirectory = layout.buildDirectory.dir('generated-resources')
}

processResources {
    dependsOn zipResourcePack
    from(layout.buildDirectory.dir('generated-resources')) {
        include 'resourcepack.zip'
    }
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
kotlin {
    jvmToolchain(21)
}

